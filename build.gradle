buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
	ivy {
		name = 'ThaumCraft 4 API'
		artifactPattern 'https://dl.dropboxusercontent.com/u/47135879/[module](-[classifier])-1.7.10-[revision](.[ext])'
	}
}

apply plugin: 'forge'

ext.configFile = file('build.properties')

ext.config = parseConfig(configFile)

version = "${config.version}-${config.build_number}"
group = "lazersmoke.botanicalworkshop" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = config.mod_name

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    assetDir = "eclipse/assets"

    //This, does the token replacement.
    //Though, I reccomend this to be replaced with a token such as @VERSION@
    replace 'GRADLE:BUILD', config.build_number
    replace 'GRADLE:VERSION', config.version

    replaceIn 'LibMisc.java' //I may have missed another file, though I can only find it in here.
}

dependencies {
    compile files(
        'Botania-deobf.jar','Baubles-deobf.jar','PlayerAPI.jar','BloodMagic-deobf.jar'
    )
	compile 'Azanor:Thaumcraft:4.2.3.5:deobf@jar'
    compile "codechicken:CodeChickenLib:1.7.10-1.1.3.136:dev"
    compile "codechicken:CodeChickenCore:1.7.10-1.0.6.44:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.4.107:dev"
}

processResources {
	//forces rerun on version change
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

/**
 * These, go outside of the processResources {}* Though, this was added with the NEI intergation commit (8bf4680)
 */
task deobfJar(type: Jar) {
    from(sourceSets.main.output)
	archiveName = "${baseName} ${version}-deobf.${extension}"
}

artifacts {
    archives deobfJar
}

task output(type: Copy) {
	from(jar.destinationDir)
    into file(config.dir_output)
}

task outputDeobf(type: Copy) {
	dependsOn "output"
	from(config.dir_output) {
		include deobfJar.archiveName
	}
    into file("${config.dir_output}/deobf")
}

task sort(type: Delete) {
	delete "${config.dir_output}/${deobfJar.archiveName}", "${config.dir_output}/wtf"
}

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

jar {
    //rename the default output, for some better... sanity with scipts
    archiveName = "${baseName} ${version}.${extension}"
}

defaultTasks 'clean', 'build', 'sort' //remove clean if you want faster builds, this will stop rare inconsistancies. 
